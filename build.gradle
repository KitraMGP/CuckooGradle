plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

// 在这里更改版本
version = '1.0.1'
group = 'com.github.zi_jing.cuckoogradle'
archivesBaseName = 'CuckooGradle'
description = 'A Gradle plugin for publishing artifacts to CuckooMaven'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.7'
}

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

shadowJar {
    classifier ''
}

javadoc.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives shadowJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        cuckooLib(MavenPublication) {
            artifact shadowJar
            artifact sourceJar
            artifact javadocJar
            pom {
                groupId = project.group
                artifactId = archivesBaseName
                version = project.version
                description = project.description
            }
        }
    }
    repositories {
        if (project.hasProperty('localPublishMavenPath')) {
            maven {
                name = 'CuckooMaven'
                url = uri(project.properties.localPublishMavenPath)
            }
        }
    }
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allSource
}

